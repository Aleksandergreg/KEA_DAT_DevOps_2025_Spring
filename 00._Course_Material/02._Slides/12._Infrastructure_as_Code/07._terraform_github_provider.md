
<div class="title-card">
    <h1>Terraform - GitHub Provider</h1>
</div>

---

# X as Code

<div>
    <img src="./assets_terraform_github_provider/X_as_Code.png" alt="X as Code" style="height: 40vh;"/>
</div>

[Source](https://youtu.be/f5EpcWp0THw?list=PLy7NrYWoggjxKDRWLqkd4Kbt84XEerHhB&t=49)

---

# A GitHub provider 

GitHub has created [a provider](https://registry.terraform.io/providers/integrations/github/6.5.0/docs) for Terraform.

With it, you can codify your GitHub infrastructure. 

---

# Prerequisites

1. Create a PAT token that has the following scopes: `repo`, `admin:org` (which implicitly contains `admin:read`).

This is different to the Read/Write permissions for GitHub Packages in CR PAT. 

2. Create a new organisation. This can only be done through the UI.

---

# Create `variables.tf`

```hcl
variable "github_token" {
  type = string
}

variable "github_org" {
  type = string
}

variable "team_members" {
  type = list(string)
}
```

---

# Define the secrets in `terraform.tfvars`


```hcl
github_token = "github_token_here"
github_org   = "name_of_the_organisation"
team_members = ["your_github_username"]
```

---

# In `main.tf`

```hcl
provider "github" {
	owner = var.github_org
	token = var.github_token
}

resource "github_repository" "repo" {
  name               = "terraform_provisioned_repo"
  description        = "Provisioned via Terraform"
  visibility         = "public"
  has_issues         = true
  has_projects       = true
  has_wiki           = true
  auto_init          = false
  license_template   = "mit"
  gitignore_template = "Python"
}

resource "github_branch_default" "default" {
	repository = github_repository.repo.name
	branch     = "main"
}

resource "github_repository_file" "readme" {
	repository          = github_repository.repo.name
	file                = "README.md"
	content             = "# Terraform Provisioned Repo \n\nThis repository was provisioned using Terraform."
	commit_message      = "Initial commit"
	overwrite_on_create = true
	branch              = "main"
}

resource "github_repository_file" "github_dir_placeholder" {
	repository          = github_repository.repo.name
	file                = ".github/.keep"
	content             = "This has been provisioned by Terraform"
	commit_message      = "Create .github directory"
	overwrite_on_create = true
	depends_on          = [github_repository_file.readme]
	branch              = "main"
}


resource "github_branch_protection" "main" {
  repository_id = github_repository.repo.node_id
  pattern       = "main"

  required_pull_request_reviews {
    required_approving_review_count = 1
    dismiss_stale_reviews           = false
    require_code_owner_reviews      = false
  }

  enforce_admins = false
}

resource "github_team" "example_team" {
  name        = "core-devs"
  description = "Core developers team"
  privacy     = "closed"
}

resource "github_team_membership" "members" {
  for_each = toset(var.team_members)
  team_id  = github_team.example_team.id
  username = each.value
  role     = "maintainer"

  depends_on = [github_team.example_team]
}
```

---

# Try to provision the resources

```bash
$ tf init
$ tf fmt
$ tf validate
$ tf plan
$ tf apply
```

Check in the UI that the:

1. [team exists](https://github.com/orgs/testterraformprovider/teams)

2. [repository exists](https://github.com/orgs/testterraformprovider/repositories)

3. [branch rule was setup](https://github.com/testterraformprovider/terraform_provisioned_repo/settings/branches)

---

# Try to destroy the resources

```bash
$ tf destroy
```

Everything will be destroyed except for the repository. 

You will see this error:

```text
â”‚ Error: DELETE https://api.github.com/repos/testterraformprovider/terraform_provisioned_repo: 403 Must have admin rights to Repository. []
```

This is not a problem with appropriate permissions or ownership but simply GitHub disallowing deleting repositories this way. Do it manually in the UI.



